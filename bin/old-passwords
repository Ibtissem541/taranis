#!/usr/bin/env perl
# This file is part of Taranis, Copyright NCSC-NL. See http://taranis.ncsc.nl
# Licensed under EUPL v1.2 or newer, https://spdx.org/licenses/EUPL-1.2.html

use strict;

use Taranis::Database;
use Taranis::FunctionalWrapper qw(Database);
use Getopt::Long;

my @users;

my $usage = <<"EOL";
Usage:
   $0 [--disable]

  --disable   disables all users with MD5 or salted SHA-512 passwords
EOL

GetOptions(
	disable => \(my $disable = 0),
	help    => \(my $help = 0),
);

if ( $help ) {
  print  "$usage\n" ;
  exit;
}

my $db = Database->{simple};

my @users = $db->query('SELECT * FROM USERS ORDER BY username')->hashes;
my @old_encryption = grep $_->{password} !~ /^\{xBCRYPT\}/, @users;

if(! @old_encryption) {
	print "INFO: No user with passwords using the old encryption.\n";
	exit 0;
}

print "Users with MD5 or salted SHA-512 passwords:\n";
foreach my $user (@old_encryption) {
	my $is_disabled = $user->{disabled} ? ' [DISABLED]' : '';
	print "   $user->{username}$is_disabled\n";
}

if(! $disable) {
	print "HINT: Use the --disable flag to disable these users.\n";
	exit 0;
}
	
foreach my $user (grep ! $_->{disabled}, @old_encryption) {
	my $name = $user->{username};

	print "disable '$name'...\n";

	$db->query( <<_DISABLE_USER, $name);
UPDATE users SET disabled = 't' WHERE username = ?
_DISABLE_USER
}

print "\n\n";
exit 0;
